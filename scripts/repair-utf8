#! /usr/bin/perl

# Repair UTF-8 containing mixed single-byte encoding, such as Latin-1

# Slightly modified from http://plasmasturm.org/log/416/


use strict;
use warnings;

use Encode qw( decode FB_QUIET );

use Getopt::Long;

my $mixed_encoding = "iso-8859-1";
GetOptions ("encoding=s" => \$mixed_encoding);

binmode STDIN, ':bytes';
binmode STDOUT, ':encoding(UTF-8)';

my $out;

while ( <> ) {
  $out = '';
  while ( length ) {
    # consume input string up to the first UTF-8 decode error
    $out .= decode( "utf-8", $_, FB_QUIET );
    # consume one character; all octets are valid Latin-1
    $out .= decode( $mixed_encoding, substr( $_, 0, 1 ), FB_QUIET ) if length;
  }
  print $out;
}
